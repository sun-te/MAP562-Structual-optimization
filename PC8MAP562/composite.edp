// Problem of a periodic cell       //
// MAP562     		   	    //
// G. Allaire, N.Spillane, T. Wick  //
//

// In this program, we solve
// a cell problem; namely a composite
// material (two phases) with the coefficients
// alpha and beta

// Material parameters, "two-phases"
// The two parameters are scalar,
// thus the two phases are isotropic.
real alpha=1.;
real beta=10.;

real cx=0.5,cy=0.5;
real r = 0.3;
real a = 0.35,b=0.2;

int n=30 ;  // mesh refinement

// Coefficients of the homogenized tensor A*
real A11, A12, A21, A22 ;
string legende ;


// Definition of the domain   //
mesh Sh;

real pi=4*atan(1) ;

//  Top boundary
border a1(t=1,0) { x=t; y=1;label=1; };
// Left boundary
border a2(t=1,0)  { x=0; y=t;label=2; };
// Bottom boundary
border a3(t=0,1) { x=t; y=0;label=3; };
// Right boundary
border a4(t=0,1) { x=1; y=t;label=4; };
// Inclusion (ellipse)
border a5(t=0,2*pi) { x=cx+a*cos(t); y=cy+b*sin(t);label=5; };

// Mesh			    //

Sh= buildmesh (a1(n)+a2(n)+a3(n)+a4(n)+a5(2*pi*0.5*(a+b)*n));
plot(Sh,wait=1);

// Define FEM spaces
// Notice the periodic boundary conditions defined using
// the labels and the periodic variable on the corresponding edge
//mesh, type of element [1,x]: edge 1 and 3 the uppper bound. if they have the same
// x value, then they will have the same value, it needs to have the same number of the nodes
// to define the symetric problem
fespace Vh1(Sh,P1,periodic=[ [1,x],[3,x],[2,y],[4,y] ]);

// Solution variables: u1 and u2
// Test function: phi
Vh1 u1,u2,phi;

// Definition of the P0 space for the coefficient
// Furthermore we define the characteristic function
fespace Vh0(Sh,P0);
Vh0 Ay , reg=region;	// Region labels
for (int i =0; i <=20 ; i++){
  cout<<Sh(i*0.05,0.5).region<<endl;
  cout<<Sh(i*0.05,0.9).region<<endl;
}
//////////////////////////////
// Definition of the coefficients for each composite
// as shown in the lecture notes

// This works since regions are labeled 0 or 1. You should check that this is the case. In more general situations make sure that in the end you have the right description in terms of the region.

Ay = alpha*reg + beta*(1.-reg) ;
plot(Sh,Ay,fill=1,wait=1,value=true);

// Defining the cell equations//

// -div_y (A(y)(e_i + \nabla_y w_i(y))) = 0
// y -> w_i(y) is Y-periodic

macro grad(u) [dx(u),dy(u)]//

problem cell1(u1,phi) =
    int2d(Sh)(Ay*grad(u1)'*grad(phi))
   +int2d(Sh)(Ay*dx(phi))
;

problem cell2(u2,phi) =
    int2d(Sh)(Ay*grad(u2)'*grad(phi))
   +int2d(Sh)(Ay*dy(phi))
;

// Solve the cell problems
cell1;
cell2;

// Compute the entries of the homogenized tensor A*
//A11 = int2d(Sh)( Ay*((dx(u1)+1.)*(dx(u1)+1.)+dy(u1)*dy(u1)) ) ;
//A22 = int2d(Sh)( Ay*((dy(u2)+1.)*(dy(u2)+1.)+dx(u2)*dx(u2)) ) ;
//A12 = int2d(Sh)( Ay*((dx(u1)+1.)*dx(u2) + dy(u1)*(dy(u2)+1.)) ) ;
//A21 = int2d(Sh)( Ay*((dx(u1)+1.)*dx(u2) + dy(u1)*(dy(u2)+1.)) ) ;

A11 = int2d(Sh)( Ay*([1,0]+grad(u1))'*([1,0]+grad(u1)) ) ;
A21 = int2d(Sh)( Ay*([1,0]+grad(u1))'*([0,1]+grad(u2)) ) ;
A12 = int2d(Sh)( Ay*([0,1]+grad(u2))'*([1,0]+grad(u1)) ) ;
A22 = int2d(Sh)( Ay*([0,1]+grad(u2))'*([0,1]+grad(u2)) ) ;


/////////////////////////////////////////
// Output and visualization            //
/////////////////////////////////////////
cout<<"Homogenized coefficients" << endl;
cout <<" A11 = " << A11 <<endl;
cout <<" A12 = " << A12 <<endl;
cout <<" A21 = " << A21 <<endl;
cout <<" A22 = " << A22 <<endl;

legende="Cell 1 problem, A11="+A11;
plot(Sh,u1,fill=1,wait=1,value=true,cmm=legende);

legende="Cell 2 problem, A22="+A22;
plot(Sh,u2,fill=1,wait=1,value=true,cmm=legende);
