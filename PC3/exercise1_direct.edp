// Construct a domain Omega
// with an inclusion omega

// In this example two disks are considered for simplicity
real R = 2;  // first radius
real r = 0.5;// second radius

int N = 10;  // Number of points on boundary

border C1(t=0,2*pi){
   x = r*cos(t);
   y = r*sin(t);
   label = 1;
}

border C2(t=0,2*pi){
   x = R*cos(t);
   y = R*sin(t);
   label = 2;
}

// build the mesh
mesh Th = buildmesh(C2(2*pi*R*N)+C1(2*pi*r*N));

// get region numbers
int Omega1 = Th(0,0).region;
int Omega2 = Th(0,0.5*(R+r)).region;

// Finite element spaces
fespace Vh(Th,P1);

// define the constant u in the problem
real u = 1;

// finite element function for the temperature and
// test function
Vh T,phi;
Vh T0 = 1; // finite element function for target temperature

macro grad(u) [dx(u),dy(u)] //  macro for gradient

// PDE for temperature
problem Temp(T,phi) =
   int2d(Th)(grad(T)'*grad(phi))
  -int2d(Th,Omega1)(u*phi)
  +on(2,T=0);

// Compute the derivative
u = 1;
Temp;

plot(T,fill=1,value = 1);

// Store it for further use
Vh T1 = T; // solution with u=1 gives derivative

// variable to store the value
real val;

real a = int2d(Th)(T1^2);
real b = int2d(Th)(T1*T0);
real c = int2d(Th)(T0^2);

// compute the objective function and store it in val
//macro J(u) {
//  val = a*u^2-2*u*b+c;
//} //

u = b/a;

//J(u);



// show the result
T = u*T1;
plot(T,fill=1,value=1,cmm="final temperature for u="+u);
